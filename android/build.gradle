buildscript {
    ext.kotlin_version = '1.9.24'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

// AGP 8+ requires every Android module to define a namespace.
// Some older Flutter plugins (in pub cache) still rely on manifest package only.
def setNamespaceFromManifestIfMissing = { Project subproject ->
    def androidExt = subproject.extensions.findByName("android")
    if (androidExt != null && androidExt.hasProperty("namespace") && !androidExt.namespace) {
        def manifestFile = file("${subproject.projectDir}/src/main/AndroidManifest.xml")
        if (manifestFile.exists()) {
            def manifest = new XmlSlurper(false, false).parse(manifestFile)
            def manifestPackage = manifest.@package?.text()
            if (manifestPackage) {
                androidExt.namespace = manifestPackage
            }
        }
    }
}

subprojects { subproject ->
    subproject.plugins.withId("com.android.library") {
        setNamespaceFromManifestIfMissing(subproject)
    }
    subproject.plugins.withId("com.android.application") {
        setNamespaceFromManifestIfMissing(subproject)
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
